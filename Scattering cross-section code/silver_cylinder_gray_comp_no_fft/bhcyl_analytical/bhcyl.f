      SUBROUTINE BHCYL(X,REFREL,T1,T2,QSCPAR,QSCPER,QEXPAR,QEXPER,
     &FIN,INTANG,ANG)
      COMPLEX I,REFREL,Y,AN,BN,A0,B0
      COMPLEX G(1000),BH(1000),T1(200),T2(200)
      DIMENSION THETA(200),ANG(200),BJ(1000),BY(1000),F(1000)
      DATA I/(0.E0,1.E0)/
      Y=X*REFREL
      XSTOP=X+4.E0*X**.3333+2.
C***
C   SERIES TERMINATED AFTER NSTOP TERMS
C***
      NSTOP=XSTOP
      YMOD=CABS(Y)
      NMX=AMAX1(XSTOP,YMOD)+15
      NPTS=INTANG+1
      DANG=FIN/FLOAT(INTANG)
      DO 0555 J=1,NPTS
      ANG(J)=(FLOAT(J)-1.E0)*DANG
 0555 THETA(J)=ANG(J)*0.017453292
C***
C  LOGARITHMIC DERIVATIVE G(J) CALCULATED BY DOWNWARD RECURRENCE
C  BEGINNING WITH INITIAL VALUE (0,1) AT J=NMX
C***
      G(NMX)=(0.E0,0.E0)
      NN=NMX-1
      DO 0120 N=1,NN
      RN=NMX-N+1
      K=NMX-N
 0120 G(K)=((RN-2.E0)/Y)-(1./(G(K+1)+(RN-1.)/Y))
C***
C  BESSEL FUNCTIONS J(N) COMPUTED BY DOWNWARD RECURRENCE BEGINNING AT
C  N=NSTOP+NDELTA
C  BESSEL FUNCTIONS Y(N) COMPUTED BY UPWARD RECURRENCE
C  BJ(N+1)=J(N), BY(N+1)=Y(N)
C***
      NDELTA=(101.+X)**.499
      MST=NSTOP+NDELTA
      MST=(MST/2)*2
      F(MST+1)=0.E0
      F(MST)=1.E-32
      M1=MST-1
      DO 0201 L=1,M1
      ML=MST-L
 0201 F(ML)=2.E0*FLOAT(ML)*F(ML+1)/X-F(ML+2)
      ALPHA=F(1)
      M2=MST-2
      DO 0202 LL=2,M2,2
 0202 ALPHA=ALPHA+2.*F(LL+1)
      M3=M2+1
      DO 0203 N=1,M3
 0203 BJ(N)=F(N)/ALPHA
      BY(1)=BJ(1)*(ALOG(.5E0*X)+.577215664)
      M4=MST/2-1
      DO 0204 L=1,M4
 0204 BY(1)=BY(1)-2.E0*((-1.E0)**L)*BJ(2*L+1)/FLOAT(L)
      BY(1)=.636619772*BY(1)
      BY(2)=BJ(2)*BY(1)-.636619772/X
      BY(2)=BY(2)/BJ(1)
      NS=NSTOP-1
      DO 0205 KK=1,NS
 0205 BY(KK+2)=2.E0*FLOAT(KK)*BY(KK+1)/X-BY(KK)
      NN=NSTOP+1
      DO 0715 N=1,NN
 0715 BH(N)=BJ(N)+I*BY(N)
      A0=G(1)*BJ(1)/REFREL+BJ(2)
      A0=A0/(G(1)*BH(1)/REFREL+BH(2))
      B0=REFREL*G(1)*BJ(1)+BJ(2)
      B0=B0/(REFREL*G(1)*BH(1)+BH(2))
      QSCPAR=CABS(B0)*CABS(B0)
      QSCPER=CABS(A0)*CABS(A0)
      DO 0101 K=1,NPTS
      T1(K)=B0
 0101 T2(K)=A0
      DO 0123 N=1,NSTOP
      RN=N
      AN=(G(N+1)/REFREL+RN/X)*BJ(N+1)-BJ(N)
      AN=AN/((G(N+1)/REFREL+RN/X)*BH(N+1)-BH(N))
      BN=(REFREL*G(N+1)+RN/X)*BJ(N+1)-BJ(N)
      BN=BN/((REFREL*G(N+1)+RN/X)*BH(N+1)-BH(N))
      DO 0102 J=1,NPTS
      C=COS(RN*THETA(J))
      T1(J)=2.E0*BN*C+T1(J)
 0102 T2(J)=2.E0*AN*C+T2(J)
      QSCPAR=QSCPAR+2.E0*CABS(BN)*CABS(BN)
 0123 QSCPER=QSCPER+2.E0*CABS(AN)*CABS(AN)
      QSCPAR=(2.E0/X)*QSCPAR
      QSCPER=(2.E0/X)*QSCPER
      QEXPER=(2.E0/X)*REAL(T2(1))
      QEXPAR=(2.E0/X)*REAL(T1(1))
      RETURN
      END
